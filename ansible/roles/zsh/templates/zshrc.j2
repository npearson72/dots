# WARNING!
# Never edit this file directly. It is managed by Ansible.

# Set global envs
export ZSH={{ role_path }}
export ZSH_FILES=$ZSH/files
export ZSH_FUNCTIONS=$ZSH/functions

typeset -U config_files
config_files=($ZSH_FILES/*.zsh)

# Load path files
for file in ${(M)config_files:#*/path.zsh}; do
  source $file
done

# Load env files
for file in ${(M)config_files:#*/env.zsh}; do
  source $file
done

# Load evertyhing else
for file in ${${config_files:#*/path.zsh}:#*/env.zsh}; do
  source $file
done

unset config_files

# Autoload custom functions
fpath=($ZSH_FUNCTIONS $fpath)

for file ($ZSH_FUNCTIONS/*); do
  if [ -f $file ]; then; autoload $file; fi
done

# Local configs
[[ -f $HOME/.zshrc.local ]] && source $HOME/.zshrc.local
